apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: "androidx.navigation.safeargs"
/*apply plugin: 'com.google.gms.google-services'
// the Crashlytics Gradle plugin
apply plugin: 'com.google.firebase.crashlytics'
// the App Distribution Gradle plugin
apply plugin: 'com.google.firebase.appdistribution'*/

//load app.properties
def appPropertiesFile = rootProject.file("app.properties")
def appPropertiesObject = new Properties()
//load all properties into appPropertiesObject
appPropertiesObject.load(new FileInputStream(appPropertiesFile))

//return a Recipes API KEY from a properties file.
def getRecipesApiKey(String property){
    Properties properties = new Properties()
    properties.load(new FileInputStream(rootProject.file("api.properties")))
    return "\"" + properties.getProperty(property) +"\""
}

android {
    def globalConfiguration = rootProject.extensions.getByName("ext")

    compileSdkVersion globalConfiguration["androidCompileSdkVersion"]
    buildToolsVersion globalConfiguration["androidBuildToolsVersion"]

    defaultConfig {
        applicationId "io.android.projectx"

        minSdkVersion globalConfiguration["androidMinSdkVersion"]
        targetSdkVersion globalConfiguration["androidTargetSdkVersion"]
        testInstrumentationRunner "io.android.projectx.presentation.test.TestRunner"

        versionCode rootProject.ext.app_version_code
        versionName rootProject.ext.app_version_name
        setProperty("archivesBaseName",
                "ProjectX_" + new Date().format("HHmm-dd-MM-yy")
                        + "_vn" + versionName + "_vc" + versionCode)
        buildConfigField("String", "RECIPES_API_KEY", getRecipesApiKey("recipesApiKey"))

        buildConfigField("String", "APP_CERTIFICATE_PINNER_KEY", "\"${app_certificate_pinner_key}\"")
        buildConfigField("String", "APP_CERTIFICATE_PINNER_VALUE", "\"${app_certificate_pinner_value}\"")
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions { jvmTarget = "1.8" }
    dexOptions { javaMaxHeapSize "3g" }
    lintOptions { abortOnError false }

    signingConfigs {
        release {
            storeFile file(appPropertiesObject['storeFile'])
            storePassword appPropertiesObject['storePassword']
            keyAlias appPropertiesObject['keyAlias']
            keyPassword appPropertiesObject['keyPassword']
        }
    }

    flavorDimensions "default"
    productFlavors {
        production {
            applicationId "io.android.projectx"
            dimension "default"
            // ConfigFields
            buildConfigField("String", "API_BASE_URL", "\"${production_api_base_url}\"")
            buildConfigField("Boolean", "APP_CERTIFICATE_IS_ENABLED", "flase")
        }
        staging {
            applicationId "io.android.projectx"
            dimension "default"
            versionNameSuffix "-staging"
            // ConfigFields
            buildConfigField("String", "API_BASE_URL", "\"${staging_api_base_url}\"")
            buildConfigField("Boolean", "APP_CERTIFICATE_IS_ENABLED", "flase")
        }
        dev {
            applicationId "io.android.projectx"
            dimension "default"
            applicationIdSuffix ".dev"
            versionNameSuffix "-dev"
            // ConfigFields
            buildConfigField("String", "API_BASE_URL", "\"${quality_api_base_url}\"")
            buildConfigField("Boolean", "APP_CERTIFICATE_IS_ENABLED", "flase")
        }
    }

    buildTypes {
        debug {
            if (applicationIdSuffix == "") {
                applicationIdSuffix ".dev"
            }
            /*signingConfig signingConfigs.release*/
            //to speed up build disable Crashlytics plugin for debug builds
            ext.enableCrashlytics = false
        }
        release {
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            /*firebaseAppDistribution {
                def ciBuildNumber = rootProject.ext.ciBuildNumber
                def versionName = rootProject.ext.app_version_name
                if (ciBuildNumber != null) {
                    serviceCredentialsFile = "./appMobile/app-customer-650g6483d2e2.json"
                    releaseNotesFile = "./appMobile/release-notes/release-notes-" + versionName + ".txt"
                    groups = "devtesters"
                }
            }*/
        }
    }

}

dependencies {
    implementation project(':Domain')
    implementation project(':Data')

    implementation project(':extensions')
    implementation project(':androidextensions')
    implementation project(':widget')

    def presentationDependencies = rootProject.ext.presentationDependencies
    def presentationTestDependencies = rootProject.ext.presentationTestDependencies

    implementation presentationDependencies.javaxAnnotation
    implementation presentationDependencies.javaxInject
    implementation presentationDependencies.rxKotlin
    implementation presentationDependencies.kotlin
    implementation presentationDependencies.rxAndroid
    implementation presentationDependencies.androidxCoreKtx
    implementation presentationDependencies.fragmentKtx
    implementation presentationDependencies.androidxAnnotations
    implementation presentationDependencies.lifecycleRuntime
    implementation presentationDependencies.lifecycleExtensions
    implementation presentationDependencies.lifecycleViewmodel
    implementation presentationDependencies.lifecycleLivedata
    implementation presentationDependencies.roomRuntime
    implementation presentationDependencies.roomRxJava
    implementation presentationDependencies.legacyV4
    implementation presentationDependencies.legacyV13
    implementation presentationDependencies.appCompat
    implementation presentationDependencies.recyclerView
    implementation presentationDependencies.constraintlayout
    implementation presentationDependencies.material
    implementation presentationDependencies.timber
    implementation presentationDependencies.glide
    implementation presentationDependencies.okHttp
    implementation presentationDependencies.okHttpLogger
    implementation presentationDependencies.moshi
    implementation presentationDependencies.navigationFragmentKtx
    implementation presentationDependencies.navigationUiKtx
    implementation presentationDependencies.navigationDynamic
    implementation presentationDependencies.firebaseAnalytics
    /*implementation presentationDependencies.firebaseCrashlytics*/
    implementation presentationDependencies.firebaseMessaging
    implementation presentationDependencies.firebaseConfig
    kapt presentationDependencies.lifecycleCompiler
    kapt presentationDependencies.roomCompiler
    // Firebase ML Kit dependencies
    implementation presentationDependencies.firebaseMlVision
    // CameraX
    implementation presentationDependencies.cameraCore
    implementation presentationDependencies.cameraCamera2

    //dagger
    implementation presentationDependencies.dagger
    kapt presentationDependencies.daggerCompiler

    implementation presentationDependencies.daggerAndroid
    implementation presentationDependencies.daggerAndroidSupport
    kapt presentationDependencies.daggerAndroidProcessor

    testImplementation presentationTestDependencies.junit
    testImplementation presentationTestDependencies.kotlinJUnit
    testImplementation presentationTestDependencies.assertj
    testImplementation presentationTestDependencies.mockito
    testImplementation presentationTestDependencies.mockitoAndroid
    testImplementation presentationTestDependencies.robolectric
    testImplementation presentationTestDependencies.archTesting
    kaptTest presentationDependencies.daggerCompiler

    // Instrumentation test dependencies
    androidTestImplementation presentationTestDependencies.junit
    androidTestImplementation presentationTestDependencies.mockito
    androidTestImplementation presentationTestDependencies.mockitoAndroid
    androidTestImplementation presentationTestDependencies.espressoCore
    androidTestImplementation presentationTestDependencies.androidxRunner
    androidTestImplementation presentationTestDependencies.androidxRules
    androidTestImplementation presentationTestDependencies.espressoIntents
    androidTestImplementation presentationTestDependencies.espressoContrib
    // Testing Navigation
    androidTestImplementation presentationTestDependencies.navigationtesting
}